/*
 * Key tool extension to manage PBE entries
 *
*/

buildscript {
	repositories {
		mavenCentral()
	}
}

repositories {
	mavenCentral()
	jcenter()
	maven {
		url 'https://plugins.gradle.org/m2/'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'application'

group = 'com.muk'
version = project.getProperty('v');
archivesBaseName = 'kt-pbe'
mainClassName = 'com.muk.app.MukMain'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.sharedManifest = manifest {
	attributes ('Created-By': 'mizuuenikaze inc.',
	'Implementation-Version': version)
}

dependencies {
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.slf4j:slf4j-log4j12:1.7.25'
	compile 'log4j:log4j:1.2.17'

	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile 'org.mockito:mockito-core:1.+'
}

pmd {
	ignoreFailures = true
}

tasks.withType(Pmd) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

tasks.withType(JavaCompile) {
	options.incremental = true
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.4.1'
}

// epack alludes to a task that packages a project to be used in a protage ebuild
task epack(dependsOn: build) {
	doLast {
		println '*****'
		println '\tProduct build ' + version
		println '*****'
	}
}

/* source artifacts */
task jarSources(type:Jar) {
	from sourceSets.main.allSource
	classifier = 'source'
}

jar.manifest {
	attributes ('Implementation-Title': 'kt-pbe')
	from sharedManifest
}

tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

distributions {
	main {
		baseName = 'kt-pbe'
	}
}

distZip.enabled = false
